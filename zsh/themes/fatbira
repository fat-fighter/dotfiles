# ZSH Theme - Preview: http://gyazo.com/8becc8a7ed5ab54a0262a470555c3eed.png
local return_code="%(?..%{$fg[red]%}%? ↵%{$reset_color%})"

if [[ $UID -eq 0 ]]; then
    local user_host='%{$terminfo[bold]$fg[red]%}%n@%m%{$reset_color%}'
    local user_symbol='#'
else
    local user_host='%{$terminfo[bold]$fg[green]%}%n@%m%{$reset_color%}'
    local user_symbol='>'
fi

function my_git_prompt() {
  tester=$(git rev-parse --git-dir 2> /dev/null) || return

  INDEX=$(git status --porcelain 2> /dev/null)
  STATUS=""

  # is branch ahead?
  if $(echo "$(git log origin/$(git_current_branch)..HEAD 2> /dev/null)" | grep '^commit' &> /dev/null); then
    STATUS="$STATUS$ZSH_THEME_GIT_PROMPT_AHEAD"
  fi

  # is branch behind?
  if $(echo "$(git log HEAD..origin/$(git_current_branch) 2> /dev/null)" | grep '^commit' &> /dev/null); then
    STATUS="$STATUS$ZSH_THEME_GIT_PROMPT_BEHIND"
  fi

  # is anything untracked?
  if $(echo "$INDEX" | grep '^?? ' &> /dev/null); then
    STATUS="$STATUS$ZSH_THEME_GIT_PROMPT_UNTRACKED"
  else
	  # is anything unstaged?
	  if $(echo "$INDEX" | command grep -E -e '^[ MARC][MD] ' &> /dev/null); then
		STATUS="$STATUS$ZSH_THEME_GIT_PROMPT_UNSTAGED"
	  # is anything staged?
	  elif $(echo "$INDEX" | command grep -E -e '^(D[ M]|[MARC][ MD]) ' &> /dev/null); then
		STATUS="$STATUS$ZSH_THEME_GIT_PROMPT_STAGED"
	  fi
  fi

  # is anything unmerged?
  if $(echo "$INDEX" | command grep -E -e '^(A[AU]|D[DU]|U[ADU]) ' &> /dev/null); then
    STATUS="$STATUS$ZSH_THEME_GIT_PROMPT_UNMERGED"
  fi

  if [[ -n $STATUS ]]; then
    STATUS=" $STATUS"
  fi

  echo "$ZSH_THEME_GIT_PROMPT_PREFIX$(git_current_branch || echo "(no branch)")$STATUS$ZSH_THEME_GIT_PROMPT_SUFFIX"
}


ZSH_THEME_GIT_PROMPT_PREFIX="%{$fg[yellow]%}‹"
ZSH_THEME_GIT_PROMPT_AHEAD="↷"
ZSH_THEME_GIT_PROMPT_BEHIND="↶"
ZSH_THEME_GIT_PROMPT_STAGED="⚫"
ZSH_THEME_GIT_PROMPT_UNSTAGED="⚪"
ZSH_THEME_GIT_PROMPT_UNTRACKED="✖"
ZSH_THEME_GIT_PROMPT_UNMERGED="↯"
ZSH_THEME_GIT_PROMPT_SUFFIX="› %{$reset_color%}"

local current_dir='%{$fg[blue]%}%~%{$reset_color%}'
local git_branch='$(my_git_prompt)%{$reset_color%}'

PROMPT="${current_dir}  ${git_branch}
%B${user_symbol}%b "
RPS1="%B${return_code}%b"
